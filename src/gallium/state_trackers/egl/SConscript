#######################################################################
# SConscript for egl state_tracker

Import('*')

env = env.Clone()

env.Append(CPPPATH = [
    '#/src/egl/main',
    '#/src/gallium/winsys/sw',
    '.',
])

sources = env.ParseSourceList('Makefile.sources', 'common_SOURCES')

if env['platform'] == 'windows':
    env.Append(CPPDEFINES = ['HAVE_GDI_BACKEND'])
    sources.append(env.ParseSourceList('Makefile.sources', 'gdi_SOURCES'))
else:
    if env['drm']:
        env.PkgUseModules('DRM')
    if env['x11']:
        env.Append(CPPDEFINES = ['HAVE_X11_BACKEND'])
        env.Prepend(CPPPATH = [
            '#/src/glx',
            '#/src/mapi',
        ])
        sources.append(env.ParseSourceList('Makefile.sources', 'x11_SOURCES'))
        if env['drm']:
            env.Append(CPPDEFINES = ['GLX_DIRECT_RENDERING'])
            sources.append(env.ParseSourceList('Makefile.sources', 'x11_drm_SOURCES'))
    if env['drm'] and False:
        # XXX: Disabled as it depends on gbm, which is not yet built with scons
        env.Append(CPPDEFINES = ['HAVE_DRM_BACKEND'])
        env.Append(CPPPATH = [
            '#/src/gbm/main',
            '#/src/gallium/state_trackers/gbm',
        ])
        sources.append(env.ParseSourceList('Makefile.sources', 'drm_SOURCES'))

st_egl = env.ConvenienceLibrary(
    target = 'st_egl',
    source = sources,
)
Export('st_egl')
